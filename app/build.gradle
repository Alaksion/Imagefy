plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id "com.google.protobuf" version "0.8.12"
}

def apiKeyPropertiesFile = rootProject.file("apikey.properties")
def apiKeyProperties = new Properties()
apiKeyProperties.load(new FileInputStream(apiKeyPropertiesFile))

android {
    compileSdk 31

    defaultConfig {
        applicationId "br.com.alaksion.myapplication"
        minSdk 23
        targetSdk 31
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
        buildConfigField("String", "PUBLIC_KEY", apiKeyProperties["PUBLIC_KEY"])
        buildConfigField("String", "SECRET_KEY", apiKeyProperties["SECRET_KEY"])
        buildConfigField("String", "REDIRECT_URI", apiKeyProperties["REDIRECT_URI"])
        buildConfigField("String", "API_SCOPE", apiKeyProperties["API_SCOPE"])
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        debug {
            testCoverageEnabled true
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = '11'
        useIR = true
    }

    buildFeatures {
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion compose_version
        kotlinCompilerVersion '1.5.31'
    }

    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

dependencies {

    implementation project(path: ':network')
    implementation project(path: ':di')

    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'com.google.android.material:material:1.4.0'
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.material:material:$compose_version"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_version"
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.4.0'
    implementation 'androidx.activity:activity-compose:1.4.0'
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_version"
    debugImplementation "androidx.compose.ui:ui-tooling:$compose_version"

    // Tests
    testImplementation "io.mockk:mockk:1.12.0"
    testImplementation "android.arch.core:core-testing:1.1.1"
    testImplementation 'androidx.test:core:1.4.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    testImplementation 'org.jetbrains.kotlin:kotlin-test'
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.5.2'


    // Hilt
    implementation 'androidx.hilt:hilt-navigation-compose:1.0.0-beta01'
    implementation 'javax.inject:javax.inject:1'
    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-compiler:$hilt_version"
    implementation "com.google.dagger:dagger:$hilt_version"
    kapt "com.google.dagger:dagger-compiler:$hilt_version"

    // Navigation
    implementation "androidx.navigation:navigation-compose:2.4.0-beta02"

    // LandScapist
    implementation 'com.github.skydoves:landscapist-glide:1.3.7'

    // Shimmer
    implementation 'com.valentinilk.shimmer:compose-shimmer:1.0.0'

    //Constraint layout
    implementation 'androidx.constraintlayout:constraintlayout-compose:1.0.0-rc01'

    //Icons
    implementation "androidx.compose.material:material-icons-extended:$compose_version"

    // Splash Provider
    implementation "androidx.core:core-splashscreen:1.0.0-alpha02"

    // Data Store
    implementation("androidx.datastore:datastore:1.0.0")
    implementation "androidx.datastore:datastore-core:1.0.0"
    implementation("androidx.datastore:datastore-preferences:1.0.0")
    implementation "com.google.protobuf:protobuf-javalite:3.10.0"

    //Accompanist
    implementation "com.google.accompanist:accompanist-systemuicontroller:0.21.2-beta"
    implementation "com.google.accompanist:accompanist-insets:0.21.2-beta"
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.10.0"
    }

    // Generates the java Protobuf-lite code for the Protobufs in this project. See
    // https://github.com/google/protobuf-gradle-plugin#customizing-protobuf-compilation
    // for more information.
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    option 'lite'
                }
            }
        }
    }
}
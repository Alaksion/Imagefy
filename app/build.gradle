plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
}

def apiKeyPropertiesFile = rootProject.file("apikey.properties")
def apiKeyProperties = new Properties()
apiKeyProperties.load(new FileInputStream(apiKeyPropertiesFile))

android {
    compileSdk 31

    defaultConfig {
        applicationId "br.com.alaksion.myapplication"
        minSdk 23
        targetSdk 31
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        vectorDrawables {
            useSupportLibrary true
        }

        buildConfigField("String", "PUBLIC_KEY", apiKeyProperties["PUBLIC_KEY"])
        buildConfigField("String", "SECRET_KEY", apiKeyProperties["SECRET_KEY"])
        buildConfigField("String", "REDIRECT_URI", apiKeyProperties["REDIRECT_URI"])
        buildConfigField("String", "API_SCOPE", apiKeyProperties["API_SCOPE"])
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        debug {
            testCoverageEnabled true
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = '11'
        useIR = true
    }

    buildFeatures {
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion = libs.versions.compose.get()
    }

    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

dependencies {

    implementation project(path: ':network')
    implementation project(path: ':core-ui')

    implementation(libs.androidx.core)
    implementation(libs.androidx.lifecycle.runtime)
    implementation("androidx.lifecycle:lifecycle-viewmodel-compose:2.4.1")

    // Tests
    testImplementation "io.mockk:mockk:1.12.3"
    testImplementation "android.arch.core:core-testing:1.1.1"
    testImplementation 'androidx.test:core:1.4.0'
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.jetbrains.kotlin:kotlin-test'
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.6.1'
    testImplementation 'app.cash.turbine:turbine:0.7.0'

    // Instrumented tests
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation(libs.androidx.compose.uijunit)
    debugImplementation(libs.androidx.compose.uimanifest)

    // Hilt
    implementation libs.hilt.core
    kapt libs.hilt.compiler

    // Navigation
    implementation "androidx.navigation:navigation-compose:2.5.0-beta01"
    implementation 'androidx.hilt:hilt-navigation-compose:1.0.0'

    //Icons
    implementation libs.androidx.compose.material.icons

    // Splash Provider
    implementation "androidx.core:core-splashscreen:1.0.0-beta02"

    // Data Store
    implementation("androidx.datastore:datastore:1.0.0")
    implementation "androidx.datastore:datastore-core:1.0.0"
    implementation("androidx.datastore:datastore-preferences:1.0.0")
    implementation "com.google.protobuf:protobuf-javalite:3.10.0"

}

kapt {
    correctErrorTypes true
}